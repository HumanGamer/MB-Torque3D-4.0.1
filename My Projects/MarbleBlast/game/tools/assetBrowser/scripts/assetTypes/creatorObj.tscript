function AssetBrowser::buildCreatorPreview(%this, %assetDef, %previewData)
{
   %class = %assetDef.val[0];
   %name = %assetDef.val[1];
   %func = %assetDef.val[2];
            
   %previewData.assetName = %name;
   %previewData.assetPath = "";
   %previewData.moduleName = "";
   
   %previewData.previewImage = "ToolsModule:" @ %class @ "_image";
   
   if(!AssetDatabase.isDeclaredAsset(%previewData.previewImage))
   {
      if(EditorIsActive())
         %previewData.previewImage = "ToolsModule:SceneObject_image";
      else if(GuiEditorIsActive())
         %previewData.previewImage = "ToolsModule:GuiControl_image";
   }
   
   //%previewData.assetFriendlyName = %assetDef.assetName;
   %previewData.assetDesc = %assetDef;
   %previewData.tooltip = "This creates a new object of the class " @ %class;
   %previewData.doubleClickCommand = %func;
}

function AssetBrowser::onFinishCreateObject(%this, %objId)
{
   %objId.position = %this.createdObjectPos;
}

function AssetBrowser::onCreatorEditorDropped(%this, %assetDef, %position)
{
   if(EditorIsActive())
   {
   %targetPosition = EWorldEditor.unproject(%position SPC 1000);
   %camPos = LocalClientConnection.camera.getPosition();
   %rayResult = containerRayCast(%camPos, %targetPosition, -1);
   
   %pos = ObjectCreator.getCreateObjectPosition();

   if(%rayResult != 0)
   {
      %pos = getWords(%rayResult, 1, 3);
   }
   else
   {
      %pos = "0 0 0";  
   }
   
   %func = %assetDef.val[2];
   
   %this.createdObjectPos = %pos;
   %newObj = eval(%func);
   }
   else if(GuiEditorIsActive())
   {
      %placeholderVar = "";
   }
}